OBTW
BASED ON THE RUBY CODE.
FUN.
TLDR

HAI 1.3

	BTW COPYD FRUM http://progopedia.com/version/loljs-1.1/
	HOW IZ I SQRUTING YR X
		BTW 
		I HAS A X_N ITZ 10
    	X_N IS NOW A NUMBAR
		I HAS A LIMIT ITZ 10
		I HAS A COUNTER ITZ 0
		IM IN YR LOOP UPPIN YR COUNTER WILE BOTH SAEM COUNTER AN SMALLR OF COUNTER AN LIMIT
            BTW VISIBLE SMOOSH "DIVIDE X=" AN X AN " AN X_N=" AN X_N MKAY
			I HAS A TERM ITZ QUOSHUNT OF X AN X_N
			TERM R SUM OF X_N AN TERM
			TERM R QUOSHUNT OF TERM AN 2
			X_N R TERM
		IM OUTTA YR LOOP
		FOUND YR X_N
	IF U SAY SO



	HOW IZ I PRIMTESTING YR NUM AN YR PRIMS AN YR NUMPRIMS

    	I IZ SQRUTING YR NUM MKAY
    	I HAS A CAP ITZ IT

    	BTW VISIBLE SMOOSH "NUM " AN NUM AN " CAP " AN CAP MKAY

		BTW LOOP THRU ALL TEH PRIMS
		IM IN YR GLOUB UPPIN YR COUNTR TIL BOTH SAEM COUNTR AN BIGGR OF COUNTR AN NUMPRIMS

			I HAS A PRIM ITZ PRIMS'Z SRS COUNTR

            DIFFRINT CAP AN BIGGR OF PRIM AN CAP, O RLY?
            YA RLY
                FOUND YR WIN
            OIC
			
			BTW IF NUM IS DVISBLE BAI ANY O F THE OTHER PRIMS, ITS N OT PRIM
			BOTH SAEM   MOD OF NUM AN PRIM   AN 0, O RLY?
			YA RLY
                BTW VISIBLE "BAI2"
				FOUND YR FAIL
			OIC
		IM OUTTA YR GLOUB

        BTW VISIBLE "BAI"
        
		FOUND YR WIN

	IF U SAY SO

	
	HOW IZ I PRIMEZGETING YR CAP

		I HAS A PRIMS ITZ A BUKKIT
    	PRIMS HAS A SRS 0 ITZ 2
		I HAS A NUMPRIMS ITZ 1
    	I HAS A FIRSTIES ITZ WIN

		BTW LOOP UNIL WEZ GOT ENUF PRIMZ
		IM IN YR LOOP UPPIN YR CUONT WILE BOTH SAEM NUMPRIMS AN SMALLR OF NUMPRIMS AN CAP
            FIRSTIES, O RLY?
            YA RLY
                CUONT R 2
                FIRSTIES R FAIL
            NO WAI
			    I IZ PRIMTESTING YR CUONT AN YR PRIMS AN YR NUMPRIMS MKAY, O RLY?
			    YA RLY
				    PRIMS HAS A SRS NUMPRIMS ITZ CUONT
                    PRIMS'Z SRS NUMPRIMS IS NOW A NUMBR  BTW FIXS WIERD MEMRY ERRUR
				    NUMPRIMS R SUM OF NUMPRIMS AN 1
			    OIC
            OIC
		IM OUTTA YR LOOP

		FOUND YR PRIMS

	IF U SAY SO

    OBTW
    VISIBLE "GIMME DA CAP"
    
	I HAS A CAP
    GIMMEH CAP
    CAP IS NOW A NUMBR
    TLDR

    I HAS A CAP ITZ 10000

    I IZ PRIMEZGETING YR CAP MKAY
    I HAS A PRIMBUKKIT ITZ IT

    IM IN YR PRINTLUP UPPIN YR INDECS TIL BOTH SAEM INDECS AN CAP
        VISIBLE PRIMBUKKIT'Z SRS INDECS
    IM OUTTA YR PRINTLUP
    
			
KTHXBYE
